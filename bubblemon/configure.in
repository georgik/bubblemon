# -*-m4-*- Process this file with autoconf to produce a configure script.

AC_INIT(src/bubblemon.h)
AM_INIT_AUTOMAKE(bubblemon, 1.0.4)
AM_CONFIG_HEADER(config.h)

# Pick up the Gnome macros.
AM_ACLOCAL_INCLUDE(macros)

GNOME_INIT(applets)
AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC

GNOME_INIT_LIBGTOP(1.0.0, fail)
GNOME_COMPILE_WARNINGS
GNOME_X_CHECKS

# Find out if the GNOME panel is resizable
AC_MSG_CHECKING(whether panel resizing is supported)
AC_EGREP_HEADER(
  change_pixel_size,
  applet-widget.h,
  have_change_pixel_size=yes,
  have_change_pixel_size=no)

if test "x$have_change_pixel_size" = "xyes"; then
  AC_MSG_RESULT(yes)
  AC_DEFINE(HAVE_CHANGE_PIXEL_SIZE)
else
  AC_MSG_RESULT(no)
fi

# For i18n and l10n
ALL_LINGUAS="sv fr pl de ko fi da sh pt ro es no"

# Check if the user wants all languages to be compiled
AC_ARG_WITH(
  all-linguas,
  [  --with-all-linguas      Compile support for all avaliable languages],
  all_linguas=yes)

if test "x$all_linguas" = "xyes"; then
  LINGUAS=$ALL_LINGUAS
fi

AC_MSG_CHECKING(what languages to include)
AC_MSG_RESULT($LINGUAS)

# For i18n and l10n (continued)
AM_GNU_GETTEXT

# Find some more or less non-standard data types
AC_CHECK_TYPE(u_int32_t, unsigned int)
AC_CHECK_TYPE(u_int16_t, unsigned short int)
AC_CHECK_TYPE(u_int8_t, unsigned char)

# Find GNOME's sysconf dir
AC_MSG_CHECKING(for GNOME's sysconf dir)
GNOME_SYSCONFDIR=`$GNOME_CONFIG --sysconfdir`
AC_SUBST(GNOME_SYSCONFDIR)
AC_MSG_RESULT($GNOME_SYSCONFDIR)

# Find GNOME's data dir
AC_MSG_CHECKING(for GNOME's data dir)
GNOME_DATADIR=`$GNOME_CONFIG --datadir`
AC_SUBST(GNOME_DATADIR)
AC_MSG_RESULT($GNOME_DATADIR)

# Subst PACKAGE_PIXMAPS_DIR.
PACKAGE_PIXMAPS_DIR="`gnome-config --datadir`/pixmaps/${PACKAGE}"
AC_SUBST(PACKAGE_PIXMAPS_DIR)

# Check if the user wants to enable ripples
AC_ARG_ENABLE(
  ripples,
  [  --enable-ripples[=amount]  Makes surface ripple whith bubbles.  Amount defaults to 0.2])

# Set rippling to 0.2 if no value is given
if test -z "$enableval"; then
  enableval="0.2"
fi

# Set rippling to 0.2 if no value is given
if test "$enableval" = "yes"; then
  enableval="0.2"
fi

AC_MSG_CHECKING(how much the bubbles should make the surface ripple)

AC_DEFINE_UNQUOTED(RIPPLES, $enableval, How much the bubbles make the surface ripple (0.0 - 1.0))

AC_MSG_RESULT($enableval)

# Check if the user wants to enable profiling
AC_ARG_ENABLE(
  profiling,
  [  --enable-profiling      Pass profiling options to the compiler],
  profiling=yes)

if test "x$profiling" = "xyes"; then
  AC_MSG_CHECKING(what profiling flags to pass to the compiler)

  profilingFLAGS="-pg"

  CFLAGS="$CFLAGS $profilingFLAGS"

  AC_MSG_RESULT($profilingFLAGS)

  AC_DEFINE(ENABLE_PROFILING)

  AC_MSG_WARN()
  AC_MSG_WARN(You have enabled profiling by specifying the --enable-profiling switch.)
  AC_MSG_WARN(Please don't install the applet the usual way; read the file PROFILING)
  AC_MSG_WARN(for instructions.)
  AC_MSG_WARN()
fi

AC_OUTPUT([
Makefile
macros/Makefile
src/Makefile
gnome/Makefile
pixmaps/Makefile
packaging/Makefile
packaging/bubblemon.spec
debian/Makefile
debian/VERSION
intl/Makefile
po/Makefile.in
po/Makefile
])
