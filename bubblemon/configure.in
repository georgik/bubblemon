# -*-m4-*- Process this file with autoconf to produce a configure script.

dnl ######################################################################
dnl General system tests required for building platform independent stuff.
dnl ######################################################################
AC_PREREQ(2.52)

AC_INIT(bubblemon,2.1.0, http://savannah.nongnu.org/bugs/?func=addbug&group=bubblemon)
AC_CONFIG_SRCDIR(src/bubblemon.h)
AM_INIT_AUTOMAKE(AC_PACKAGE_NAME, AC_PACKAGE_VERSION)
AM_CONFIG_HEADER(config.h)

AC_PATH_PROG(GCONFTOOL, gconftool-2)
AC_PROG_INTLTOOL(0.22)

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC

dnl Find some more or less non-standard data types
AC_CHECK_TYPE(u_int32_t, unsigned int)
AC_CHECK_TYPE(u_int16_t, unsigned short int)
AC_CHECK_TYPE(u_int8_t, unsigned char)

dnl Find out whether we are on a big-endian system
AC_C_BIGENDIAN

dnl ######################################################################
dnl Gettext stuff.
dnl ######################################################################
GETTEXT_PACKAGE=bubblemon
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE",[Gettext name])
ALL_LINGUAS="sv fr pl de ko fi da sh pt ro es no hu it is ru"
AM_GLIB_GNU_GETTEXT

dnl Check if the user wants all languages to be compiled
AC_ARG_WITH(
  all-linguas,
  [  --with-all-linguas      Compile support for all avaliable languages],
  all_linguas=yes)

if test "x$all_linguas" = "xyes"; then
  LINGUAS=$ALL_LINGUAS
fi

AC_MSG_CHECKING(what languages to include)
AC_MSG_RESULT($LINGUAS)

dnl #########################################################################
dnl Set PACKAGE_LOCALE_DIR in config.h.
dnl #########################################################################
if test "x${prefix}" = "xNONE"; then
   AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${ac_default_prefix}/${DATADIRNAME}/locale",[fix])
else
   AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${prefix}/${DATADIRNAME}/locale",[fix])
fi

dnl #########################################################################
dnl GNOME 2.x specific tests
dnl #########################################################################
dnl Versions required
LIBPANEL_REQUIRED=2.0.0
LIBGTOP_REQUIRED=2.0.0
LIBGNOMEUI_REQUIRED=2.0.0
GLIB_REQUIRED=2.0.0
LIBGNOMEPRINTUI_REQUIRED=1.115.0
LIBGLADE_REQUIRED=0.17

dnl We need the panel for the applets
build_gpanel=false;
PKG_CHECK_MODULES(GNOME_APPLETS, 
	[libpanelapplet-2.0 >= $LIBPANEL_REQUIRED],
	build_gpanel=true,
	AC_MSG_WARN([GNOME2 panel not found.]))
AC_SUBST(GNOME_APPLETS_CFLAGS)
AC_SUBST(GNOME_APPLETS_LIBS)

dnl Check for gtop2 libs
build_gtop_applets=false
PKG_CHECK_MODULES(GTOP_APPLETS,
	[libgtop-2.0 >= $LIBGTOP_REQUIRED],
	build_gtop_applets=true,
	AC_MSG_WARN([libgtop not found.]))
AC_SUBST(GTOP_APPLETS_CFLAGS)
AC_SUBST(GTOP_APPLETS_LIBS)
AM_CONDITIONAL(BUILD_GTOP_APPLETS, $build_gtop_applets)

dnl Check for glade libs
build_libglade=false
PKG_CHECK_MODULES(LIBGLADE,
	[libglade-2.0 libgnomeui-2.0 >= $LIBGLADE_REQUIRED],
	build_libglade=true,
	AC_MSG_WARN([libglade not found.]))
AC_SUBST(LIBGLADE_CFLAGS)
AC_SUBST(LIBGLADE_LIBS)
AM_CONDITIONAL(BUILD_LIBGLADE, $build_libglade)

dnl Check for pkg-config required libs
build_bubblemon=false
PKG_CHECK_MODULES(BUBBLEMON,
	[libgnomeui-2.0 >= $LIBGNOMEUI_REQUIRED 
	glib-2.0 >= $GLIB_REQUIRED
	libgnomeprintui-2.0 >= $LIBGNOMEPRINTUI_REQUIRED],
	build_bubblemon=true,
	AC_MSG_WARN([needed libraries not found.]))
AC_SUBST(BUBBLEMON_CFLAGS)
AC_SUBST(BUBBLEMON_LIBS)

dnl #########################################################################
dnl GConf related settings
dnl #########################################################################
GCONF_CONFIG_SOURCE=
AC_ARG_ENABLE(gconf-source, 
	[  --enable-gconf-source=sourceaddress Where to install schema files.], 
	GCONF_CONFIG_SOURCE=$enable_gconf_source,)

if test "x$GCONF_CONFIG_SOURCE" = "x"; then
   GCONF_CONFIG_SOURCE="xml::\${sysconfdir}/gconf/gconf.xml.defaults"
fi

AC_SUBST(GCONF_CONFIG_SOURCE)
AC_MSG_CHECKING("GConf configuration source")
AC_MSG_RESULT(${GCONF_CONFIG_SOURCE})


dnl #########################################################################
dnl Output our test results
dnl #########################################################################

AC_OUTPUT([
Makefile
src/Makefile
gnome/Makefile
help/Makefile
help/C/Makefile
omf-install/Makefile
pixmaps/Makefile
packaging/Makefile
packaging/bubblemon.spec
po/Makefile.in
po/Makefile
man/Makefile
man/bubblemon-gnome2.1
man/hu/Makefile
man/hu/bubblemon-gnome2.1
man/sv/Makefile
man/sv/bubblemon-gnome2.1
])
